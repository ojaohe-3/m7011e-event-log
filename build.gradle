plugins {
    id 'java-library'
    id 'groovy'
    id 'idea'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.3.5'
    id 'org.liquibase.gradle' version '2.2.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.hibernate.orm' version '6.5.3.Final'
    id 'org.graalvm.buildtools.native' version '0.10.3'
    id "pl.allegro.tech.build.axion-release" version "1.18.7"
}


ext {
    springCloudVersion = "2023.0.3"
}

scmVersion {
    versionIncrementer('incrementPatch')
    ignoreUncommittedChanges.set(false)
    tag {
        versionIncrementer('incrementPatch')
        prefix.set('v')

    }
}

group = 'm7011e.the-homeric-odyssey'
project.version = scmVersion.version


subprojects.each {
    check.dependsOn("${it.name}:check")
    publish.dependsOn("${it.name}:publish")
    assemble.dependsOn("${it.name}:assemble")
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'org.springframework.boot'

    group = rootProject.group
    project.version = rootProject.version

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        }
    }
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

allprojects {
    project.version = rootProject.version

    apply from: "$rootDir/gradle/repository.gradle"

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        bom
        compileOnly.extendsFrom(bom)
        annotationProcessor.extendsFrom(bom)
        implementation.extendsFrom(bom)
    }

    dependencies {
        implementation platform(group: 'm7011e.the-homeric-odyssey', name: 'common-dependencies', version: '0.1.0')
        api 'm7011e.the-homeric-odyssey:resource-server'
        api 'm7011e.the-homeric-odyssey:authentication-components'

        implementation platform('org.springframework.boot:spring-boot-dependencies:3.4.0')
        implementation platform('org.springframework.cloud:spring-cloud-dependencies:2023.0.3')

        implementation 'org.springframework.boot:spring-boot-starter-data-rest'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation group: 'io.openapitools.swagger', name: 'swagger-maven-plugin', version: '2.1.6'
        implementation(group: 'org.modelmapper', name: 'modelmapper', version: '3.2.1')
        implementation group: 'commons-io', name: 'commons-io', version: '2.17.0'
        implementation group: 'org.modelmapper', name: 'modelmapper', version: '3.2.1'
        implementation group: 'commons-validator', name: 'commons-validator', version: '1.9.0'

        compileOnly 'org.projectlombok:lombok:1.18.36'
        annotationProcessor 'org.projectlombok:lombok:1.18.36'

    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'


    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.apache.groovy:groovy:4.0.24'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.9'
    testImplementation 'com.athaydes:spock-reports:2.5.1-groovy-4.0'

    api(project(":event-log-models"))
    api(project(":event-log-orm"))
}


project(":event-log-models") {
    dependencies {
        api 'org.springframework.cloud:spring-cloud-starter-openfeign'

    }
}



project(":event-log-orm") {
    apply plugin: 'org.liquibase.gradle'

    configurations {
        liquibase
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.liquibase:liquibase-core'
        api(project(":event-log-models"))
    }
}

hibernate {
    enhancement {
        enableAssociationManagement = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
